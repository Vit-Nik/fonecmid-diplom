
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Ответственный = Пользователи.ТекущийПользователь();
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		ЗаполнитьНаОснованииЗаказаПокупателя(ДанныеЗаполнения);
	КонецЕсли;
	
	// Доработка Задача "Работа с заявками клиентов" 10.04.2025
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СтрокаТаблицыЗначений") Тогда
		НовыйДокумент = ДанныеЗаполнения.ДокументРеализация;
		ВыполнитьАвтозаполнение(НовыйДокумент, ДанныеЗаполнения);
	КонецЕсли;
	
	// Конец доработка Задача "Работа с заявками клиентов" 10.04.2025
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	СуммаДокумента = Товары.Итог("Сумма") + Услуги.Итог("Сумма");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьНаОснованииЗаказаПокупателя(ДанныеЗаполнения)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаказПокупателя.Организация КАК Организация,
	               |	ЗаказПокупателя.Контрагент КАК Контрагент,
	               |	ЗаказПокупателя.Договор КАК Договор,
	               |	ЗаказПокупателя.СуммаДокумента КАК СуммаДокумента,
	               |	ЗаказПокупателя.Товары.(
	               |		Ссылка КАК Ссылка,
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура КАК Номенклатура,
	               |		Количество КАК Количество,
	               |		Цена КАК Цена,
	               |		Сумма КАК Сумма
	               |	) КАК Товары,
	               |	ЗаказПокупателя.Услуги.(
	               |		Ссылка КАК Ссылка,
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура КАК Номенклатура,
	               |		Количество КАК Количество,
	               |		Цена КАК Цена,
	               |		Сумма КАК Сумма
	               |	) КАК Услуги
	               |ИЗ
	               |	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	               |ГДЕ
	               |	ЗаказПокупателя.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
	
	ТоварыОснования = Выборка.Товары.Выбрать();
	Пока ТоварыОснования.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Товары.Добавить(), ТоварыОснования);
	КонецЦикла;
	
	УслугиОснования = Выборка.Услуги.Выбрать();
	Пока ТоварыОснования.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Услуги.Добавить(), УслугиОснования);
	КонецЦикла;
	
	Основание = ДанныеЗаполнения;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.ОбработкаЗаказов.Записывать = Истина;
	Движения.ОстаткиТоваров.Записывать = Истина;
	
	Движение = Движения.ОбработкаЗаказов.Добавить();
	Движение.Период = Дата;
	Движение.Контрагент = Контрагент;
	Движение.Договор = Договор;
	Движение.Заказ = Основание;
	Движение.СуммаОтгрузки = СуммаДокумента;

	Для Каждого ТекСтрокаТовары Из Товары Цикл
		Движение = Движения.ОстаткиТоваров.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Номенклатура = ТекСтрокаТовары.Номенклатура;
		Движение.Сумма = ТекСтрокаТовары.Сумма;
		Движение.Количество = ТекСтрокаТовары.Количество;
	КонецЦикла;
	
КонецПроцедуры

// Параметы
// Объект - Документ ссылка РеализацияТоваровУслуг
// ДанныеДляЗаполнения - Данные по выполненным работам по абонентскому договору
Процедура ВыполнитьАвтозаполнение(Объект, ДанныеДляЗаполнения) Экспорт
	// Доработка Задача "Работа с заявками клиентов" 03.04.2025
	
	Объект.Услуги.Очистить();
	
	НоменклатураАбонентскаяПлата = Константы.ВКМ_НоменклатураАбонентскаяПлата.Получить();
	НоменклатураРаботыСпециалиста = Константы.ВКМ_НоменклатураРаботыСпециалиста.Получить();
	
	Если ДанныеДляЗаполнения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НоменклатураАбонентскаяПлата) ИЛИ Не ЗначениеЗаполнено(НоменклатураРаботыСпециалиста)Тогда
		ОбщегоНазначения.СообщитьПользователю("Необходимо задать значение номенклатуры абонентской платы и номенклатуры работы специалиста");
		Возврат;
	КонецЕсли;
	
	Объект.Основание = ДанныеДляЗаполнения.Договор;
	
	АбонентскаяПлата = ДанныеДляЗаполнения.АбонентскаяПлата;
	
	Если ЗначениеЗаполнено(АбонентскаяПлата) Тогда
		НоваяСтрока = Объект.Услуги.Добавить();
		НоваяСтрока.Номенклатура = НоменклатураАбонентскаяПлата;
		НоваяСтрока.Количество = 1;
		НоваяСтрока.Сумма = АбонентскаяПлата;
	КонецЕсли;
	
	Если ДанныеДляЗаполнения.Сумма = 0 Тогда
		Возврат;
	Иначе
		НоваяСтрока = Объект.Услуги.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеДляЗаполнения);
		НоваяСтрока.Номенклатура = НоменклатураРаботыСпециалиста;
	КонецЕсли;
	
	// Конец доработка Задача "Работа с заявками клиентов" 03.04.2025
КонецПроцедуры 
 
Функция ДанныеОВыполненныхРаботахПоДоговору(АбонентскийДоговор, Объект) Экспорт
// Доработка Задача "Работа с заявками клиентов" 04.04.2025
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_ВыполненныеКлиентуРаботыОбороты.Договор КАК Договор,
		|	СУММА(ЕСТЬNULL(ВКМ_ВыполненныеКлиентуРаботыОбороты.КоличествоЧасовПриход, 0)) КАК Количество,
		|	СУММА(ЕСТЬNULL(ВКМ_ВыполненныеКлиентуРаботыОбороты.СуммаКОплатеПриход, 0)) КАК Сумма,
		|	ДоговорыКонтрагентов.ВКМ_АбонентскаяПлата КАК АбонентскаяПлата
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВКМ_ВыполненныеКлиентуРаботы.Обороты(&НачалоМесяца, &КонецМесяца, Регистратор, Договор = &Договор) КАК ВКМ_ВыполненныеКлиентуРаботыОбороты
		|		ПО (ВКМ_ВыполненныеКлиентуРаботыОбороты.Договор = ДоговорыКонтрагентов.Ссылка)
		|ГДЕ
		|	ДоговорыКонтрагентов.Ссылка = &Договор
		|	И ДоговорыКонтрагентов.ВКМ_НачалоПериодаДействия <= &Дата
		|	И ДоговорыКонтрагентов.ВКМ_ОкончаниеПериодаДействия >= &Дата
		|
		|СГРУППИРОВАТЬ ПО
		|	ВКМ_ВыполненныеКлиентуРаботыОбороты.Договор,
		|	ДоговорыКонтрагентов.ВКМ_АбонентскаяПлата";
	
	Запрос.УстановитьПараметр("Договор", АбонентскийДоговор);
	Запрос.УстановитьПараметр("Дата", Объект.Дата);
	Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(Объект.Дата));
	Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(Объект.Дата));
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Возврат РезультатЗапроса;

// Конец доработка Задача "Работа с заявками клиентов" 04.04.2025
КонецФункции

#КонецОбласти

#КонецЕсли

